%%

id= (\+|\^)(CMTI|CPIN|CMEE|BOOT|RSSI|SIMST|SRVST|SYSINFO|CDSI|CMGS|MODE|CPBS)

idMultiline= (\+|\^)(CNUM)
idPdu= (\+|\^)(CMGR|CMGL|CDS|CMT)

finalResultCode= OK|CONNECT|RING|NO\sCARRIER|NO\sDIALTONE|BUSY|NO\sANSWER|COMMAND\sNOT\sSUPPORT|TOO\sMANY\sPARAMETERS 

%%

<>\r\n {

        if( this.expectPdu ){

                this.expectPdu= false; 

                this.pushState("PDU");
        }
                

        return "SEP";
}

<>{id} {

        this.jjval= this.jjtext;

        return "ID";

}

<>{idMultiline} {

        this.jjval= this.jjtext;

        return "ID_MULTILINE";

}

<>{idPdu} {

        this.expectPdu= true;

        this.jjval= this.jjtext;

        if( this.jjval === "+CMGL" ) return "CMGL";

        return "ID_PDU";

}

<>\+CME\sERROR:\s[0-9]+ {

        this.jjval= parseInt(this.jjtext.match(/^\+CME\ ERROR:\ ?([0-9]+)$/)[1]);
                
        return "CME_ERROR";
}

<>\+CMS\sERROR:\s[0-9]+ {

        this.jjval= parseInt(this.jjtext.match(/^\+CMS\ ERROR:\ ?([0-9]+)$/)[1]);
                
        return "CMS_ERROR";

}

<>ERROR { return "ERROR"; }

<>{finalResultCode} {

        this.jjval= this.jjtext;

        return "FINAL_RESULT_CODE";
}

<> A\/\r { 

        this.jjval= this.jjtext;

        return "ECHO";
}

<>AT { this.pushState("ECHO"); }

<> >\x20 { return "INVITE"; }


<>[^\r] { 

        this.prevChar= this.jjtext;

        this.pushState("REST"); 
        
}

<REST>[^\r]*/\r {

        this.jjval= this.prevChar + this.jjtext;

        this.popState();

        return "REST";

}

<PDU>\r\n {

        this.popState();
                
        return "SEP";

}

<PDU>[a-fA-F0-9]+ {

        this.jjval= this.jjtext;

        return "PDU";
}

<ECHO> [^\r]*\r {

        this.jjval= "AT" + this.jjtext;

        this.popState();

        return "ECHO";

}